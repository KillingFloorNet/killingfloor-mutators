Class ZEDManagerMut extends Info;

var array< class<ZEDSoldierBase> > Zeds;
var KFGameType KF;
var int StartingWave,HardIndex;
var byte IndexCounter;
var bool bHasInit;

function PostBeginPlay()
{
	KF = KFGameType(Level.Game);
	SetTimer(30,false);
}

// Every 1-3 minutes add one ZED soldier.
function Timer()
{
	if( !bHasInit )
	{
		StartingWave = Min(KF.FinalWave*0.5f,KF.FinalWave-1);
		HardIndex = Min(KF.FinalWave*0.8f,KF.FinalWave-1);
		Log("Adding ZED soldiers after wave"@StartingWave$"/"$KF.FinalWave,Class.Outer.Name);
		bHasInit = true;
	}

	SetTimer(60+FRand()*120.f,false);
	if( !KF.bWaveInProgress || KF.WaveNum<StartingWave || KF.WaveNum>=KF.FinalWave || KF.TotalMaxMonsters<=0 || HasZEDSoldier() )
		return;

	if( IndexCounter<Zeds.Length && FRand()<0.6f )
		++IndexCounter; // Add harder ones as game goes by.

	AddOne();

	if( KF.WaveNum>=HardIndex && FRand()<0.5f ) // Sometimes spawns 2-3 at once.
	{
		AddOne();
		if( FRand()<0.25 )
			AddOne();
	}

	KF.LastZVol = KF.FindSpawningVolume();
	KF.LastSpawningVolume = KF.LastZVol;
}

final function AddOne()
{
	KF.NextSpawnSquad.Insert(0,1);
	KF.NextSpawnSquad[0] = Zeds[Rand(IndexCounter)];
}

final function bool HasZEDSoldier() // Limit to max 2 at once.
{
	local Controller C;
	local byte i;

	for( C=Level.ControllerList; C!=None; C=C.nextController )
		if( !C.bIsPlayer && ZEDSoldierBase(C.Pawn)!=None && ++i==2 )
			return true;
	return false;
}

defaultproperties
{
     Zeds(0)=Class'ZEDSoldiersB.ZEDSoldier'
     Zeds(1)=Class'ZEDSoldiersB.ZEDBerserker'
     Zeds(2)=Class'ZEDSoldiersB.ZEDCommando'
     Zeds(3)=Class'ZEDSoldiersB.ZEDSniper'
     Zeds(4)=Class'ZEDSoldiersB.ZEDFlamer'
     Zeds(5)=Class'ZEDSoldiersB.ZEDDemo'
     Zeds(6)=Class'ZEDSoldiersB.ZEDMario'
     IndexCounter=1
}
